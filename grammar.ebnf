(* Lleuad grammar *)
(*
    This is an EBNF document with alterations
    ebnf: https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form
    templates are defined with `$...` in the name.

    This document is used to design the syntax of the language and to provide
        A spec to base the parser on. This document will not be used to generate
        The parser.

    Terminals like Names, Strings, and Numbers aren't defined here.
    How whitespace is handled also isn't defined here.
*)

root = {declaration, ";"} EOF;

comment = "//", {ANYTHING}, "\n";

(* quite common *)
(* left associative, an accumulator should be preferred over recursion *)
ident = name | ident, ".", name;

(* toplevel syntax *)
declaration = use_stmt | function_def | type_def | variable_def;

statement = (declaration | func_call | assignment | loop_stmt | if_stmt), ";";

(* declaration statements *)
use_stmt = "use", import_path, ["as", name];

type_def = "type", [type_parameter_list], name, "=",
         ( type_expression | type_derivation );

variable_def = ("local" | "const"), ident, [":", type_expression],
               "=", expression

function_def = ["public"], "function", [type_parameter_list], function_ident
                parameter_list, [":", type_expression],
                "begin", {statement}, "end";

assignment = ident, "=", expression;
return = "return", expression;

(* syntax unique to declarations *)
import_path = import_path_head {".", import_path_item};
import_path_head = "library", "(", name, ")" | name;

type_derivation = "struct", "{", fields, "}";
(* extra statements *)
loop_stmt = ("for", name, "in", expression | "while", expression), "do",
            {statement}, "end";
if_stmt = "if", expression, "then",{statement},
          ["else", {statement}],
          "end";

(* a template for generating list inners *)
list_inner($a...) = [$a..., {",", $a...}, [","]];

parameter_list = "(", list_inner(name, [":", type_exression]), ")";

type_parameter_list = "[", list_inner(name, [":", type_expression]), "]";

(* value expressions *)
expression_attribute = (indent | func_call | "(", expression, ")"),
                       [".",  name];
func_call = expression_attribute, "(", list_inner(expression), ")"; 

(* these represent the lowest part of an expression tree *)
expression_atom = ident | number | string;
expression_nest = expression_atom |  expression_attribute;

(* prefix operators *)
expression_negate     = ["-"], expression_nest;
(* infix operators *)
expression_factor     = expression_negate,
            [("*"|"/"), expression factor];
expression_sum        = expression_factor,
            [("+"|"-"), expression sum];
expression_comparison = expression_sum,
            [("<"|">"|"<="|">="|"=="|"!="), expression_sum];
expression_precombinative = ["!"], expression_comparison;
expression_combinative = expression_precombinative,
            [("&" | "|"), expression_combinative];

expression = undefined;
(* type expressions *)
type_expression = name, ["[", list_inner(type_expression), "]"];
